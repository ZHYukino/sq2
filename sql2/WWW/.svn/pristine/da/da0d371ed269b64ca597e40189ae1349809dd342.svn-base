<%
'==================================================================
'函数名称：ReadIni()  WriteIni()
'函数参数：ReadIni (FilePath_Name,MySession,MyItem)
'          WriteIni(FilePath_Name,MySession,MyItem,MyValue)
'函数作用：读写INI文件
'函数版本：Version 1.1
'返 回 值：对于读文件函数，找到给定项返回其值，找不到则返回空字符串；
'          写文件函数无返回值，如何函数查找不到对应的节点或项，会自动创建
'调用说明：使用 <!--#include File="RWIniFile.inc"--> 包含本文件
'          CStr_Temp = ReadIni("./Test.ini","Colors","Color1")   //读一个值到CStr_Temp变量
'          Call  WriteIni("./Test.ini","Colors","Color1",1)      //更改Color1项的值为1
'注意事项：函数严格区分大小写；所读写的INI文件必须是ANSI编码，不支持Unicode编码
'作    者：优化设计
'时    间：2008-04-20 01:32
'腾 讯 QQ：50199907
'作者网站：小小程序员 (http://www.yan-wei.net)
'欢迎转载，转载请注明出处，并保留此版权，如果您完善了此函数，请给我发一份：yw1530@126.com，谢谢
'==================================================================

'=============读INI文件=============
'参数：FilePath_Name 文件相对路径；MySession 节点名称；MyItem 项名称
'返回值：找到给定项返回其值，找不到则返回空字符串
Function ReadIni(FilePath_Name,MySession,MyItem)
	Dim MyString, MyArray,str_temp,sesstion_temp

	Set  MyfileObject=Server.CreateObject("Scripting.FileSystemObject")   
	Set  MytextFile=MyfileObject.OpenTextFile(Server.MapPath("/")&FilePath_Name) 

	sesstion_temp=""
	While Not MytextFile.AtendOfStream   
	    Str_temp= MytextFile.ReadLine
				
		If Trim(Str_temp)<>"" and InStr(Trim(Str_temp),";")<>1 Then
		
			If InStr(Trim(Str_temp),"[")<InStr(Trim(Str_temp),"]") Then
				sesstion_temp=Trim(Str_temp)
				sesstion_temp=Replace(Trim(sesstion_temp),"[","")
				sesstion_temp=Replace(Trim(sesstion_temp),"]","")
			End If
		 
			MyArray = Split(Trim(Str_temp), "=")
			If Trim(MyArray(0))=MyItem and sesstion_temp=MySession then
				ReadIni= Trim(MyArray(1))
				Exit Function
			End if
			
		End if
	Wend   
	
	Mytextfile.Close
	ReadIni=""
	
	Set  MyfileObject=Nothing
	Set  MytextFile=Nothing
End Function

'=============写INI文件=============
'参数：FilePath_Name 文件相对路径；MySession 节点名称；MyItem 项名称；MyValue 写入值
Function WriteIni(FilePath_Name,MySession,MyItem,MyValue)
	Dim MyString, MyArray,str_temp,FindBool,FindBool_Session
	FindBool=False
	FindBool_Session=False

	Set  MyfileObject=Server.CreateObject("Scripting.FileSystemObject")   
	Set  MytextFile=MyfileObject.OpenTextFile(Server.MapPath(FilePath_Name)) 

	sesstion_temp=""
	While Not MytextFile.AtendOfStream   
	    Str_temp= MytextFile.ReadLine

		If Trim(Str_temp)<>"" and InStr(Trim(Str_temp),";")<>1 Then 
		
			If InStr(Trim(Str_temp),"[")<InStr(Trim(Str_temp),"]") Then
				sesstion_temp=Trim(Str_temp)
				sesstion_temp=Replace(Trim(sesstion_temp),"[","")
				sesstion_temp=Replace(Trim(sesstion_temp),"]","")
				
				If sesstion_temp=MySession  then
					FindBool_Session=True
				End if
			End If
		
			MyArray = Split(Trim(Str_temp), "=")
			If Trim(MyArray(0))=MyItem and sesstion_temp=MySession  then
				FindBool=True
			End if
			
		End if
	Wend   
	
	Mytextfile.Close
	

	If Not FindBool_Session Then 
		FindBool_Session=True
		MyString=MyString & "[" & MySession & "]" & VbCrLf
		Set  MytextFile=MyfileObject.OpenTextFile(Server.MapPath(FilePath_Name))
			MyString=MyString & MytextFile.ReadAll
		Mytextfile.Close
		
		Set MytextFile=MyfileObject.CreateTextFile(Server.MapPath(FilePath_Name)) 
			MytextFile.WriteLine(MyString)
		MytextFile.Close 	
	End If


	Set  MytextFile=MyfileObject.OpenTextFile(Server.MapPath(FilePath_Name)) 
	
	MyString=""
	sesstion_temp=""
	
	While Not MytextFile.AtendOfStream   
	    Str_temp= MytextFile.ReadLine
		
		If Trim(Str_temp)<>"" Then 
		
			If InStr(Trim(Str_temp),"[")<InStr(Trim(Str_temp),"]") Then
				sesstion_temp=Trim(Str_temp)
				sesstion_temp=Replace(Trim(sesstion_temp),"[","")
				sesstion_temp=Replace(Trim(sesstion_temp),"]","")

				MyString=MyString & VbCrLf
			End If
		
			MyArray = Split(Trim(Str_temp), "=")
			If Trim(MyArray(0))=MyItem and sesstion_temp=MySession  then
				MyArray(1)=MyValue
				Str_temp=MyArray(0) & "=" & MyArray(1)
			End if
			
			If MyString=VbCrLf then 
				MyString=MyString & Str_temp
			Else
				MyString=MyString & VbCrLf & Str_temp
			End If
			
			If Not FindBool and sesstion_temp=MySession  Then 
				FindBool=True
				MyString=MyString & VbCrLf & MyItem & "=" & MyValue
			End If
			
		End if
	Wend   
	
	Mytextfile.Close
	 
	Set MytextFile=MyfileObject.CreateTextFile(Server.MapPath(FilePath_Name)) 
		MytextFile.WriteLine(MyString)
	MytextFile.Close 

	Set  MyfileObject=Nothing
	Set  MytextFile=Nothing
End Function
%>