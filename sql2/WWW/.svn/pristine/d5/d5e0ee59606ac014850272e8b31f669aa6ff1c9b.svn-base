<!--#include file="../comm/mconn.asp"-->
<%
	Session.CodePage = 65001
	Response.Charset = "UTF-8"
	itype = Request.QueryString("itype") '1 按日期段 2 按月份 3 按年份
	'offset = Request.QueryString("offset") '页码
	'limit = Request.QueryString("limit") '每页显示条数
	begindate = Request.QueryString("date1") '开始时间
	enddate = Request.QueryString("date2") '结束时间
	tablename = Request.QueryString("tablename")
	flag = 1
	Select Case itype
		Case 1
			If IsDate(begindate) And IsDate(enddate) Then
				begindate = FormatDateTime(CDate(begindate),2)
				enddate = FormatDateTime(DateAdd("d",1,CDate(enddate)),2)
			Else
				flag = 0
			End if
		Case 2
			If IsDate(begindate) Then
				begindate = FormatDateTime(CDate(begindate),2)
				enddate = FormatDateTime(DateAdd("m",1,begindate),2)
			Else
				flag = 0
			End if
		Case 3
			If IsDate(begindate & "-01") Then
				begindate = FormatDateTime(CDate(begindate & "-01"),2)
				enddate = FormatDateTime(DateAdd("yyyy",1,begindate),2)
			Else
				flag = 0
			End if
		Case Else
			flag = 0
	End Select
	If flag = 1 Then
		Dim fs,filename,myfile,rstData
		Set fs = server.CreateObject("scripting.filesystemobject")
		If tablename = "" Then tablename = "Book1"
		filename = Server.MapPath("../uploadfile/" + tablename + ".xls")
		If fs.FileExists(filename) Then
			fs.DeleteFile(filename)
		End If
		Set myfile = fs.CreateTextFile(filename,true)
		sql = "select top 1000 * from LogSys where LogTime>='" & begindate & "' and LogTime<'" & enddate & "' order by LogTime desc"
		Set rstData = Conn.Execute(sql)
		If Not rstData.EOF And Not rstData.BOF Then
			strLine = ""
			strLine = strLine & "日志时间" & chr(9)
			strLine = strLine & "登录帐号" & chr(9)
			strLine = strLine & "日志类型" & chr(9)
			strLine = strLine & "日志内容" & chr(9)
			strLine = strLine & "备注" & chr(9) 
			myfile.writeline strLine
			Do While Not rstData.EOF
				strLine = ""
				strLine = strLine & FormatDateTime(rstData("LogTime"),0) & chr(9)
				strLine = strLine & rstData("logman") & chr(9)
				strLine = strLine & rstData("logtype") & chr(9)
				strLine = strLine & rstData("Logdesc") & chr(9)
				strLine = strLine & rstData("LogMemo") & chr(9)
				myfile.writeline strLine
				rstData.MoveNext
			Loop
			rstData.close
			Set rstData = nothing
		End If
%>
		<script>window.open("../uploadfile/<%=tablename%>.xls")</script>
<%
	Else
		Response.write "参数错误"
	End If
%>
